apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'java'

compileJava.options.encoding = 'UTF-8'

mainClassName = 'gamer.App'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'commons-cli', name: 'commons-cli', version: '1.2+'
  testCompile group: 'junit', name: 'junit', version: '4.+'
}

def getVersionName = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

jar {
  manifest {
    attributes 'Main-Class': 'gamer.App'
    attributes 'Implementation-Version': getVersionName()
  }
}

// Create a single jar with all dependencies.
task fatjar(type: Jar) {
  manifest {
    attributes 'Main-Class': 'gamer.App'
    attributes 'Implementation-Version': getVersionName()
  }
  baseName = project.name + '-all'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}
