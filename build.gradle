apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'java'

def getVersionName = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--dirty'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

compileJava.options.encoding = 'UTF-8'
mainClassName = 'gamer.App'
sourceCompatibility = '1.7'
version = getVersionName()

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'commons-cli', name: 'commons-cli', version: '1.2+'
  testCompile group: 'junit', name: 'junit', version: '4.12+'
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
    attributes 'Implementation-Version': version
  }
}

// Create a single jar with all dependencies.
task fatjar(type: Jar) {
  baseName = project.name + '-all'
  doFirst {
    manifest {
      attributes 'Main-Class': mainClassName
      attributes 'Implementation-Version': version
    }
  }
  from {
    configurations.compile.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  with jar
}

run {
  if (project.hasProperty('args'))  {
    args project.args.split('\\s')
  }
}

